apply plugin: "com.android.application"
apply plugin: "me.tatarka.retrolambda"
apply plugin: "jacoco-android"
apply from: rootProject.file("gradle/jacoco.gradle")

android {
    signingConfigs {
        debug {
            storeFile file("debug.keystore")
        }
    }
    compileSdkVersion 26
    buildToolsVersion "26.0.1"
    defaultConfig {
        applicationId "com.aleksandersh.weather"
        minSdkVersion 17
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
        debug {
            testCoverageEnabled true
            minifyEnabled false
            signingConfig signingConfigs.debug
        }
    }
    packagingOptions {
        exclude "META-INF/rxjava.properties"
    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
    sourceSets {
        def prodFiles = new File("$projectDir/src/main/java")
        def testFiles = new File("$projectDir/src/testSources/java/")
        androidTest {
            java {
                srcDirs += testFiles
                prodFiles.eachFileRecurse {
                    if (testFiles.list().contains(it.name)) {
                        srcDirs -= it
                    }
                }
            }
        }
        test {
            java.srcDirs += "src/testUtils/java"
        }
    }
}

ext {
    supportLibraryVersion = "26.0.0"
    butterknifeVersion = "8.7.0"
    retrofitVersion = "2.3.0"
    daggerVersion = "2.11"
    constraintLayoutVersion = "1.0.2"
    firebaseJobDispatcherVersion = "0.7.0"
    rxAndroidVersion = "2.0.1"
    rxJavaVersion = "2.1.1"
    rxBindingVersion = "2.0.0"
    junitVersion = "4.12"
    testRunnerVersion = "1.0.0"
    mockitoVersion = "2.8.9"
    espressoVersion = "3.0.0"
    robolectricVersion = "3.4"
    okhttp3IdlingResourceVersion = "1.0.0"
    stethoVersion = "1.5.0"
    autocompleteVersion = "1.0.1"
    roomVersion = "1.0.0-alpha5"
    timberVersion = "4.5.1"
    leakCanaryVersion = "1.5.2"
    podamVersion = "7.1.0.RELEASE"
}

dependencies {
    def androidTestCompileExclude = { String lib, Map<String, String> additionalExcludedModules = null ->
        androidTestCompile(lib) {
            exclude group: "com.android.support"
            exclude module: "support-annotations"
            exclude module: "support-core-ui"
            exclude module: "recyclerview-v7"
            if (additionalExcludedModules != null) exclude(additionalExcludedModules)
        }
    }

    // Room
    compile "android.arch.persistence.room:runtime:$roomVersion"
    compile "android.arch.persistence.room:rxjava2:$roomVersion"
    annotationProcessor "android.arch.persistence.room:compiler:$roomVersion"

    // Butterknife
    compile "com.jakewharton:butterknife:$butterknifeVersion"
    annotationProcessor "com.jakewharton:butterknife-compiler:$butterknifeVersion"

    // Support
    compile "com.android.support:support-v4:$supportLibraryVersion"
    compile "com.android.support:appcompat-v7:$supportLibraryVersion"
    compile "com.android.support:preference-v7:$supportLibraryVersion"
    compile "com.android.support:design:$supportLibraryVersion"
    compile "com.android.support:support-annotations:$supportLibraryVersion"
    compile "com.android.support:cardview-v7:$supportLibraryVersion"
    compile "com.android.support:recyclerview-v7:$supportLibraryVersion"
    compile "com.android.support.constraint:constraint-layout:$constraintLayoutVersion"

    // Retrofit
    compile "com.squareup.retrofit2:retrofit:$retrofitVersion"
    compile "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    compile "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"

    // Dagger
    compile "com.google.dagger:dagger:$daggerVersion"
    annotationProcessor "com.google.dagger:dagger-compiler:$daggerVersion"

    // Rx
    compile "io.reactivex.rxjava2:rxjava:$rxJavaVersion"
    compile "io.reactivex.rxjava2:rxandroid:$rxAndroidVersion"
    compile "com.jakewharton.rxbinding2:rxbinding:$rxBindingVersion"
    compile "com.jakewharton.rxbinding2:rxbinding-recyclerview-v7:$rxBindingVersion"

    // Misc
    compile "com.firebase:firebase-jobdispatcher:$firebaseJobDispatcherVersion"
    compile "com.jakewharton.timber:timber:$timberVersion"

    // Debug

    debugCompile "com.facebook.stetho:stetho:$stethoVersion"
    debugCompile "com.facebook.stetho:stetho-okhttp3:$stethoVersion"

//    debugCompile "com.squareup.leakcanary:leakcanary-android:$leakCanaryVersion"
//    compile "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"
//    testCompile "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"
//    releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"
//    androidTestCompile "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"

    // Testing

    testCompile "org.mockito:mockito-core:$mockitoVersion"
    testCompile "org.robolectric:robolectric:$robolectricVersion"

    testCompile "junit:junit:$junitVersion"

    testCompile "uk.co.jemos.podam:podam:$podamVersion"

    androidTestCompileExclude "com.android.support.test:runner:$testRunnerVersion"
    androidTestCompileExclude "com.android.support.test.espresso:espresso-core:$espressoVersion"
    androidTestCompileExclude "com.android.support.test.espresso:espresso-contrib:$espressoVersion"
    compile "com.android.support.test.espresso:espresso-idling-resource:$espressoVersion"
    compile("com.jakewharton.espresso:okhttp3-idling-resource:$okhttp3IdlingResourceVersion", {
        exclude module: "okio"
        exclude module: "okhttp"
    })

}





